apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        if (file("signing.gradle").exists()) {
            apply from: 'signing.gradle'
        }
    }

    defaultConfig {
        applicationId "com.wire.testinggallery"
        minSdkVersion 17
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 99999
        versionName "6.1." + versionCode
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    flavorDimensions "default"

    productFlavors {
        qa {
            dimension "default"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:27.0.0"
    implementation 'com.google.code.gson:gson:2.2.4'
    implementation 'com.google.guava:guava:19.0'
}

//add pretty naming to apk filename
android.applicationVariants.all { variant ->
    def appName = "testing_gallery"

    variant.outputs.each { output ->
        def newApkName
        newApkName = "${appName}-${output.baseName}.apk"
        output.outputFileName = new File("../..", newApkName)
    }
}
